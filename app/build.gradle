apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        applicationId "n7.ad2"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode gitCommitCount()
        versionName getGitVersionMessage()
//        versionCode 1
//        versionName "1"

        dataBinding {
            enabled = true
        }
    }

    signingConfigs {
        release {
            storeFile file("C:\\Users\\i30mb1\\AndroidProjects\\key111111.jks")
            storePassword "111111"
            keyAlias "key"
            keyPassword "111111"
        }
    }

    buildTypes {
        debug {
            resValue("string", "PORT_NUMBER", "8081")
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            minifyEnabled true
        }
    }

    lintOptions {
        // If set to true, turns off analysis progress reporting by lint.
        quiet true
        // if set to true (default), stops the build if errors are found.
        abortOnError true
        // if true, only report errors.
        ignoreWarnings true
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def gitCommitCount() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        def commitCount = stdout.toString().trim().toInteger()
        return commitCount + 330
    }
    catch (ignored) {
        return 0
    }
}

def getGitVersionMessage() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '-1', '--pretty=%B'
            standardOutput = stdout
        }
        def commitCount = stdout.toString().trim()
        return commitCount
    }
    catch (ignored) {
        return "1"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
//     Animated TextView
    implementation 'com.robinhood.ticker:ticker:1.2.2'
    // Navigation
    implementation 'com.yarolegovich:sliding-root-nav:1.1.0'
    // cmd ipconfig основной шлюз + :8080 (как узнать страничку для браузера)
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'
    // Lib
    implementation files('libs/twirc-0.2.jar')
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    // WorkManager
    implementation "android.arch.work:work-runtime:1.0.1"
    // Room components
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    // Lifecycle components
    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    // Paging
    implementation "android.arch.paging:runtime:1.0.1"
//     Android
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'jp.wasabeef:picasso-transformations:2.2.1'
    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'
    // Jsoup для разбора html
    implementation 'org.jsoup:jsoup:1.11.2'
    //поддержка jsoup для старых устройств
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-messaging:17.6.0'

    implementation 'com.google.android.gms:play-services-ads:17.2.0'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.10.2'
    implementation project(':twitch')
    implementation 'com.android.support:multidex:1.0.3'

    // auto update
    implementation 'com.google.android.play:core:1.6.1'
}

apply plugin: 'com.google.gms.google-services'
